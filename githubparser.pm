package modules::local::githubparser;
use strict;
use warnings;
use XML::Atom::Client;
use HTML::Entities;

# Parse ATOM generated by github.com.

my $url;
my $lastrev;
my $copy_of_self;
my $lr_var_name;
my $module_name;
my @targets;

sub init {
    my $self = shift;
    $copy_of_self = $self;
    $url          = $$self{url};
    $module_name  = $$self{module_name};
    $lr_var_name  = $$self{lastrev_var_name};
    $lr_var_name  = $module_name."_lastvar" unless defined $lr_var_name;
    my $rev = main::get_item($self, $lr_var_name);
    undef $rev unless length $rev;
    $lastrev = $rev if defined $rev;
    main::lprint("$module_name: init: initialized lastrev to $lastrev") if defined $lastrev;
    main::create_timer($module_name."_fetch_feed_timer", $$self{modulename}, "fetch_feed", 30);
}

sub implements {
    return qw();
}

sub shutdown {
    my $self = shift;
    main::delete_timer($module_name."_fetch_feed_timer");
    main::store_item($self, $lr_var_name, $lastrev) if defined $lastrev;
}

sub fetch_feed {
    my $self = $copy_of_self;
    my $atom = XML::Atom::Client->new();
    my $feed = $atom->getFeed($url);
    $self->process_feed($feed);
}

sub process_feed {
    my ($self, $feed) = @_;
    my @items = $feed->entries;
    @items = sort { $a->updated cmp $b->updated } @items; # ascending order
    my $newest = $items[-1];
    my $latest = $newest->updated;
    $latest = $items[0]->updated if exists $ENV{TEST_RSS_PARSER};

    # skip the first run, to prevent new installs from flooding the channel
    if(defined($lastrev)) {
        # output new entries to channel
        foreach my $item (@items) {
            my $updated = $item->updated;
            $self->output_item($item) if $updated gt $lastrev;
        }
    }
    $lastrev = $latest;
    main::store_item($copy_of_self, $lr_var_name, $lastrev);
}

sub longest_common_prefix {
    my $prefix = shift;
    for (@_) {
        chop $prefix while (! /^\Q$prefix\E/);
    }
    return $prefix;
}


sub output_item {
    my ($self, $item) = @_;
    my $prefix  = 'unknown';
    my $creator = $item->author;
    if(defined($creator)) {
        $creator = $creator->name;
    } else {
        $creator = 'unknown';
    }
    my $link    = $item->link->href;
    my $desc    = $item->content;
    if(defined($desc)) {
        $desc = $desc->body;
    } else {
        $desc = '(no commit message)';
    }

    $creator = "($creator)" if($creator =~ /\s/);

    my ($rev)   = $link =~ m|/commit/([a-z0-9]{40})|;
    my ($log, $files);
    $desc =~ s/^.*<pre>//;
    $desc =~ s/<\/pre>.*$//;
    my @lines = split("\n", $desc);
    my @files;
    while($lines[0] =~ /^m (.+)/) {
        push(@files, $1);
        shift(@lines);
    }
    return main::lprint("$module_name: error parsing filenames from description")
        unless $lines[0] eq '';
    shift(@lines);
    pop(@lines) if $lines[-1] =~ /^git-svn-id: http:/;
    pop(@lines) while scalar(@lines) && $lines[-1] eq '';
    $log = join("\n", @lines);

    $prefix =  longest_common_prefix(@files);
    $prefix =~ s|^/||;      # cut off the leading slash
    if(scalar @files > 1) {
        $prefix .= " (" . scalar(@files) . " files)";
    }

    $log =~ s|<br */>||g;
    decode_entities($log);
    my @log_lines = split(/[\r\n]+/, $log);

    $rev = substr($rev, 0, 7);
    $self->put("$module_name: $rev | $creator++ | $prefix:");
    foreach my $line (@log_lines) {
	$self->put("$module_name: $line");
    }
    $self->put("$module_name: review: $link");
    main::lprint("$module_name: output_item: output rev $rev");
}

sub put {
    my ($self, $line) = @_;
    foreach my $target (@{$$self{targets}}) {
        main::send_privmsg(@$target, $line);
    }
}

1;
